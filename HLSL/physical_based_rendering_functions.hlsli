//#include"shading_functions.hlsli"

//	円周率
static const float PI = 3.141592654f;

//	ガンマ係数
static const float GammaFactor = 2.2f;

//--------------------------------------------
//	フレネル項
//--------------------------------------------
//F0	: 垂直入射時の反射率
//VdotH	: 視線ベクトルとハーフベクトル（光源へのベクトルと視点へのベクトルの中間ベクトル
float3 CalcFresnel(float3 F0, float VdotH)
{
	return F0 + (1.0f - F0) * pow(clamp(1.0 - VdotH, 0.0f, 1.0f), 5.0f);
}

//--------------------------------------------
//	拡散反射BRDF(正規化ランバートの拡散反射)
//--------------------------------------------
//VdotH		: 視線へのベクトルとハーフベクトルとの内積
//fresnelF0	: 垂直入射時のフレネル反射色
//diffuse_reflectance	: 入射光のうち拡散反射になる割合
float3  DiffuseBRDF(float VdotH, float3 fresnelF0, float3 diffuse_reflectance)
{
	return (1.0f - CalcFresnel(fresnelF0, VdotH)) * (diffuse_reflectance / PI);
}

//--------------------------------------------
//	法線分布関数
//--------------------------------------------
//NdotH		: 法線ベクトルとハーフベクトル（光源へのベクトルと視点へのベクトルの中間ベクトル）の内積
//roughness : 粗さ
float  CalcNormalDistributionFunction(float NdotH, float roughness)
{
	float a = roughness * roughness;
	float b = (NdotH * NdotH) * (a - 1.0f) + 1.0f;
	return a / (PI * b * b);
}

//--------------------------------------------
//	幾何減衰項の算出
//--------------------------------------------
//NdotL		: 法線ベクトルと光源へのベクトルとの内積
//NdotV		: 法線ベクトルと視線へのベクトルとの内積
//roughness : 粗さ
float CalcGeometryFunction(float NdotL, float NdotV, float roughness)
{
	float r = roughness * 0.5f;
	float shadowing = NdotL / (NdotL * (1.0 - r) + r);
	float masking = NdotV / (NdotV * (1.0 - r) + r);
	return shadowing * masking;
}

float GeometrySchlickGGX(float NdotV, float roughness)
{
	float r = (roughness + 1.0);
	float k = (r * r) / 8.0;
	return NdotV / (NdotV * (1.0 - k) + k);
}

float GeometrySmithGGX(float NdotV, float NdotL, float roughness)
{
	float gv = GeometrySchlickGGX(NdotV, roughness);
	float gl = GeometrySchlickGGX(NdotL, roughness);
	return gv * gl;
}

//	鏡面反射BRDF（クック・トランスのマイクロファセットモデル）
//--------------------------------------------
//NdotV		: 法線ベクトルと視線へのベクトルとの内積
//NdotL		: 法線ベクトルと光源へのベクトルとの内積
//NdotH		: 法線ベクトルとハーフベクトルとの内積
//VdotH		: 視線へのベクトルとハーフベクトルとの内積
//fresnelF0	: 垂直入射時のフレネル反射色
//roughness	: 粗さ
float3 SpecularBRDF(float NdotV, float NdotL, float NdotH, float VdotH, float3 fresnelF0, float roughness)
{
	//	D項(法線分布)
	float D = CalcNormalDistributionFunction(NdotH, roughness);
	//	G項(幾何減衰項)
	float G = CalcGeometryFunction(NdotL, NdotV, roughness);
	//	F項(フレネル反射)
	float3 F = CalcFresnel(fresnelF0, VdotH);

	return D * G * F / (NdotL * NdotV * 4.0);
}

//walterモデルの反射
float3 SpecularWalterBRDF(float NdotV, float NdotL, float NdotH, float VdotH, float3 F0, float roughness)
{
	float D = CalcNormalDistributionFunction(NdotH, roughness);
	float G = GeometrySmithGGX(NdotV, NdotL, roughness);
	float3 F = CalcFresnel(F0, VdotH);

	return (D * G * F) / (4.0 * NdotL * NdotV);
}

//Burleyの拡散モデル
float3 BurleyDiffuseBRDF(float NdotV, float NdotL, float VdotH, float3 diffuse_reflectance, float roughness)
{
	float Fd90 = 0.5 + 2.0 * roughness * VdotH * VdotH;
	float lightScatter = 1.0 + (Fd90 - 1.0) * pow(1.0 - NdotL, 5.0);
	float viewScatter = 1.0 + (Fd90 - 1.0) * pow(1.0 - NdotV, 5.0);

	return diffuse_reflectance * (lightScatter * viewScatter) / PI;
}

//Oren-Nayar 拡散BRDF
float3 OrenNayarDiffuse(float3 N, float3 V, float3 L, float3 diffuseColor, float roughness)
{
	float NdotL = saturate(dot(N, L));
	float NdotV = saturate(dot(N, V));

	float3 H = normalize(V + L);
	float VdotH = saturate(dot(V, H));

	float sigma = roughness * roughness;
	float sigma2 = sigma * sigma;

	float A = 1.0 - (0.5 * sigma2 / (sigma2 + 0.33));
	float B = 0.45 * sigma2 / (sigma2 + 0.09);

	float3 Lperp = normalize(L - N * NdotL);
	float3 Vperp = normalize(V - N * NdotV);

	float cos_phi_diff = saturate(dot(Lperp, Vperp)); // φの差の余弦

	float alpha = max(NdotL, NdotV);
	float beta = min(NdotL, NdotV);

	return diffuseColor * (A + B * cos_phi_diff * beta) / PI;
}

//--------------------------------------------
//	ルックアップテーブルからGGX項を取得
//--------------------------------------------
//brdf_sample_point	: サンプリングポイント
//lut_ggx_map       : GGXルックアップテーブル
//state             : 参照時のサンプラーステート
float4 SampleLutGGX(float2 brdf_sample_point, Texture2D lut_ggx_map, SamplerState state)
{
	return lut_ggx_map.Sample(state, brdf_sample_point);
}

//--------------------------------------------
//	キューブマップから照度を取得
//--------------------------------------------
//v                     : サンプリング方向
//diffuse_iem_cube_map  : 事前計算拡散反射IBLキューブマップ
//state                 : 参照時のサンプラーステート
float4 SampleDiffuseIEM(float3 v, TextureCube diffuse_iem_cube_map, SamplerState state)
{
	return diffuse_iem_cube_map.Sample(state, v);
}

//--------------------------------------------
//	キューブマップから放射輝度を取得
//--------------------------------------------
//v	                        : サンプリング方向
//roughness                 : 粗さ
//specular_pmrem_cube_map   : 事前計算鏡面反射IBLキューブマップ
//state                     : 参照時のサンプラーステート
float4 SampleSpecularPMREM(float3 v, float roughness, TextureCube specular_pmrem_cube_map, SamplerState state)
{
	//ミップマップによって粗さを表現するため、段階を算出
	uint width, height, mip_maps;
	specular_pmrem_cube_map.GetDimensions(0, width, height, mip_maps);
	float lod = roughness * float(mip_maps - 1);
	return specular_pmrem_cube_map.SampleLevel(state, v, lod);
}

//--------------------------------------------
//	粗さを考慮したフレネル項の近似式
//--------------------------------------------
//F0	    : 垂直入射時の反射率
//VdotN 	: 視線ベクトルと法線ベクトルとの内積
//roughness	: 粗さ
float3 CalcFresnelRoughness(float3 f0, float NdotV, float roughness)
{
	return f0 + (max((float3)(1.0f - roughness), f0) - f0) * (saturate(1.0f - NdotV), 5.0f);
}

//--------------------------------------------
//	拡散反射IBL
//--------------------------------------------
//normal                : 法線(正規化済み)
//eye_vector		    : 視線ベクトル(正規化済み)
//roughness				: 粗さ
//diffuse_reflectance	: 入射光のうち拡散反射になる割合
//F0					: 垂直入射時のフレネル反射色
//diffuse_iem_cube_map  : 事前計算拡散反射IBLキューブマップ
//state                 : 参照時のサンプラーステート
float3 DiffuseIBL(float3 normal, float3 eve_vector, float roughness, float3 diffuse_reflectance, float3 f0, TextureCube diffuse_iem_cube_map, SamplerState state)
{
	float3 N = normal;
	float3 V = -eve_vector;

	//間接拡散反射光の反射率
	float NdotV = max(0.0001f, dot(N, V));
	float3 kD = 1.0f - CalcFresnelRoughness(f0, NdotV, roughness);

	float3 irradiance = SampleDiffuseIEM(normal, diffuse_iem_cube_map, state).rgb;
	return diffuse_reflectance * irradiance * kD;
}

//簡易用のDiffuseIBL,テクスチャの変わりにライトカラーで対応
float3 SimpleDiffuseIBL(float3 normal,float3 eve_vector,float roughness,float3 diffuse_reflectance, float3 f0,float3 LC)
{
	float3 N = normal;
	float3 V = -eve_vector;

	//間接拡散反射光の反射率
	float NdotV = max(0.0001f, dot(N, V));
	float3 kD = 1.0f - CalcFresnelRoughness(f0, NdotV, roughness);
	return diffuse_reflectance * LC * kD;
}

//--------------------------------------------
//	鏡面反射IBL
//--------------------------------------------
//normal				    : 法線ベクトル(正規化済み)
//eye_vector			    : 視線ベクトル(正規化済み)
//roughness				    : 粗さ
//F0					    : 垂直入射時のフレネル反射色
//lut_ggx_map               : GGXルックアップテーブル
//specular_pmrem_cube_map   : 事前計算鏡面反射IBLキューブマップ
//state                     : 参照時のサンプラーステート
float3 SpecularIBL(float3 normal, float3 eve_vector, float roughness, float3 f0, Texture2D lut_ggx_map, TextureCube specular_pmrem_cube_map, SamplerState state)
{
	float3 N = normal;
	float3 V = -eve_vector;

	float NdotV = max(0.0001f, dot(N, V));
	float3 R = normalize(reflect(-V, N));
	float3 specular_light = SampleSpecularPMREM(R, roughness, specular_pmrem_cube_map, state).rgb;

	float2 brdf_sample_point = saturate(float2(NdotV, roughness));
	float2 env_brdf = SampleLutGGX(brdf_sample_point, lut_ggx_map, state).rgb;
	return specular_light * (f0 * env_brdf.x + env_brdf.y);
}

//float3 SimpleSpecularIBL(float3 normal, float3 eve_vector, float roughness, float3 f0)
//{
//	float3 N = normal;
//	float3 V = -eve_vector;
//
//	float NdotV = max(0.0001f, dot(N, V));
//	float3 R = normalize(reflect(-V, N));
//
//
//}

//--------------------------------------------
//	直接光の物理ベースライティング
//--------------------------------------------
//diffuse_reflectance	: 入射光のうち拡散反射になる割合
//F0					: 垂直入射時のフレネル反射色
//normal				: 法線ベクトル(正規化済み)
//eye_vector			: 視点に向かうベクトル(正規化済み)
//light_vector			: 光源に向かうベクトル(正規化済み)
//light_color			: ライトカラー
//roughness				: 粗さ
void DirectBRDF(float3 diffuse_reflectance,
	float3 F0,
	float3 normal,
	float3 eye_vector,
	float3 light_vector,
	float3 light_color,
	float roughness,
	out float3 out_diffuse,
	out float3 out_specular)
{
	float3 N = normal;
	float3 L = -light_vector;
	float3 V = -eye_vector;
	float3 H = normalize(L + V);

	float NdotV = max(0.0001f, dot(N, V));
	float NdotL = max(0.0001f, dot(N, L));
	float NdotH = max(0.0001f, dot(N, H));
	float VdotH = max(0.0001f, dot(N, H));

	float3 irradiance = light_color * NdotL;

	//拡散反射BRDF
	out_diffuse = DiffuseBRDF(VdotH, F0, diffuse_reflectance) * irradiance;
	//out_diffuse = BurleyDiffuseBRDF(NdotV, NdotL, VdotH, diffuse_reflectance, roughness) * irradiance;
	//out_diffuse = OrenNayarDiffuse(N, V, L, diffuse_reflectance, roughness) * irradiance;

	//	鏡面反射BRDF
	out_specular = SpecularBRDF(NdotV, NdotL, NdotH, VdotH, F0, roughness) * irradiance;
	//out_specular = SpecularWalterBRDF(NdotV, NdotL, NdotH, VdotH, F0, roughness) * irradiance;
}